apply plugin: 'maven'

ext.optionalDeps = []
ext.providedDeps = []

ext.optional = { optionalDeps << it }
ext.provided = { providedDeps << it }

// exclude hadoop install sub projects
def hadoopInstalls=['cdh5','hadoop22','hadoop24','hdp21','phd1','phd20']
if (hadoopInstalls.contains(project.name)) {
	// Disable installing hadoop distro artifacts
	install.enabled = false
	// Disable publishing hadoop distro artifacts
	project.tasks.findByPath("artifactoryPublish")?.enabled = false
}

install {
	repositories.mavenInstaller {
		customizePom(pom, project)
	}
}

def customizePom(pom, gradleProject) {
	pom.whenConfigured { generatedPom ->
		// respect 'optional' and 'provided' dependencies
		gradleProject.optionalDeps.each { dep ->
			generatedPom.dependencies.find { it.artifactId == dep.name }?.optional = true
		}
		gradleProject.providedDeps.each { dep ->
			generatedPom.dependencies.find { it.artifactId == dep.name }?.scope = 'provided'
		}

		// eliminate test-scoped dependencies (no need in maven central poms)
		generatedPom.dependencies.removeAll { dep ->
			dep.scope == 'test'
		}

		// add all items necessary for maven central publication
		generatedPom.project {
			name = gradleProject.description
			description = gradleProject.description
			url = linkHomepage
			organization {
				name = 'Spring by Pivotal'
				url = 'https://spring.io'
			}
			licenses {
				license {
					name 'The Apache Software License, Version 2.0'
					url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					distribution 'repo'
				}
			}
			scm {
				url = linkScmUrl
				connection = linkScmConnection
				developerConnection = linkScmDevConnection
			}

			dependencyManagement {
				dependencies {
					dependency {
						groupId = 'io.spring.platform'
						artifactId = 'platform-bom'
						version = platformVersion
						scope = 'import'
						type = 'pom'
					}
				}
			}

			developers {
				developer {
					id = 'markfisher'
					name = 'Mark Fisher'
					email = 'mfisher@gopivotal.com'
				}
				developer {
					id = 'markpollack'
					name = 'Mark Pollack'
					email = 'mpollack@gopivotal.com'
				}
				developer {
					id = 'garyrussell'
					name = 'Gary Russell'
					email = 'grussell@gopivotal.com'
				}
				developer {
					id = 'ghillert'
					name = 'Gunnar Hillert'
					email = 'ghillert@gopivotal.com'
				}
				developer {
					id = 'dturanski'
					name = 'David Turanski'
					email = 'dturanski@gopivotal.com'
				}
				developer {
                                        id = 'ebottard'
                                        name = 'Eric Bottard'
                                        email = 'ebottard@gopivotal.com'
                                }
				developer {
                                        id = 'grenfro'
                                        name = 'Glenn Renfro'
                                        email = 'grenfro@gopivotal.com'
                                }
				developer {
                                        id = 'igopinathan'
                                        name = 'Ilayaperumal Gopinathan'
                                        email = 'igopinathan@gopivotal.com'
                                }
				developer {
                                        id = 'trisberg'
                                        name = 'Thomas Risberg'
                                        email = 'trisberg@gopivotal.com'
                                }
				developer {
					id = 'jhickey'
					name = 'Jennifer Hickey'
				}
				developer {
					id = 'mminella'
					name = 'Michael Minella'
					email = 'mminella@gopivotal.com'
				}
				developer {
					id = 'ltaylor'
					name = 'Luke Taylor'
					email = 'ltaylor@gopivotal.com'
				}
				developer {
					id = 'aclement'
					name = 'Andy Clement'
					email = 'aclement@gopivotal.com'
				}
				developer {
					id = 'dsyer'
					name = 'Dave Syer'
					email = 'dsyer@gopivotal.com'
				}
				developer {
					id = 'jvalkealahti'
					name = 'Janne Valkealahti'
					email = 'jvalkealahti@gopivotal.com'
				}
				developer {
					id = 'jbrisbin'
					name = 'Jon Brisbin'
					email = 'jbrisbin@gopivotal.com'
				}
				developer {
					id = 'tdarimont'
					name = 'Thomas Darimont'
					email = 'tdarimont@gopivotal.com'
				}
			}
		}
	}
}
